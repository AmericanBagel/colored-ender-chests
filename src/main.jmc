@import "src/functions/*";

/////// Ender Chest
Item.create(
    ender_chest,
    item_frame,
    "&<!italic>Ender Chest",
    [
        "&<!italic,blue>Colored Ender Chests",
        "&<!italic,dark_gray>colored_ender_chests:give/ender_chest"
    ],
    {
        CustomModelData:17210202,
        colored_ender_chests: {
            isItem:1b,
            isEnderChest:1b,
            type:"item",
            id:"colored_ender_chests:ender_chest"
            },
        EntityTag:{
            Item: {
                id:"minecraft:item_frame",
                Count:1b,
                tag:{
                    CustomModelData:17210202
                }
            },
            Tags: [
                "colored_ender_chests.ender_chest.item_frame"
            ]
        }
    }

);

Recipe.table(
    {
        "type": "minecraft:crafting_shaped",
        "pattern": [
            "/W/",
            "O#O",
            "/E/"
        ],
        "key": {
            "/": {
                "item": "minecraft:blaze_rod"
            },
            "W": {
                "tag": "minecraft:wool"
            },
            "O": {
                "item": "minecraft:obsidian"
            },
            "#": {
                "item": "minecraft:ender_chest"
            },
            "E": {
                "item": "minecraft:end_crystal"
            }
        },
        "result": {
            "item": "minecraft:knowledge_book",
            "count": 1
        }
    },
    knowledge_book,
    () => {
        Item.give(ender_chest, @s, 1);
        advancement revoke @s from colored_ender_chests:ender_chest/craft;
        recipe take @s colored_ender_chests:ender_chest;
        clear @s knowledge_book 1;
    }
);

new tags.item(dye) {
    "values": [
        "minecraft:white_dye",
        "minecraft:light_gray_dye",
        "minecraft:gray_dye",
        "minecraft:black_dye",
        "minecraft:brown_dye",
        "minecraft:red_dye",
        "minecraft:yellow_dye",
        "minecraft:lime_dye",
        "minecraft:green_dye",
        "minecraft:cyan_dye",
        "minecraft:light_blue_dye",
        "minecraft:blue_dye",
        "minecraft:purple_dye",
        "minecraft:magenta_dye",
        "minecraft:pink_dye"
    ]
}

class ender_chest {
    // Run in __tick__ as item frame
    function place() {
        execute align xyz positioned ~.5 ~ ~.5 run {
            playsound block.stone.place block @a ~ ~ ~ 1 0.8;
            summon armor_stand ~ ~1 ~ {
                Tags: [
                    "colored_ender_chests.ender_chest.marker"
                ],
                Pose: {Head:[0f,0f,0f]},
                ArmorItems: [{},{},{},{
                    id: "minecraft:item_frame",
                    Count: 1b,
                    tag: {CustomModelData:17210200}
                    }],
                NoGravity:1b,
                Marker:1b,
                Invisible:1b
                }
            setblock ~ ~ ~ barrel{CustomName:'{"text":"Ender Chest","italic":false}'}
            kill @s;
        }
    }

    function destroy.noncreative_mined() {
        say "noncreative_mined";
    }

    function destroy() {
        if (entity @e[type=item,distance=..0.5,nbt={colored_ender_chests:{isPlayerMined:1b}}]) {
            ender_chest.destroy.noncreative_mined();
        }

        stopsound @a[distance=..16] block block.wood.break;
        playsound block.stone.break block @a ~ ~ ~ 1 0.8;

        kill @s;
    }
}

function __tick__() {
    // Invisible Minecarts
    execute as @e[
            type=#minecraft:minecarts,
            tag=!invisible_minecart
            ]
        run invisible_minecarts();

    execute as @e[
        type=item_frame,
        tag=colored_ender_chests.ender_chest.item_frame
        ]
        at @s
        run ender_chest.place();

    JMC.put('execute as @e[tag=colored_ender_chests.ender_chest.marker] at @s unless block ~ ~-1 ~ barrel unless block ~ ~ ~ barrel unless block ~ ~1 ~ barrel run function colored_ender_chests:ender_chest/destroy');
}